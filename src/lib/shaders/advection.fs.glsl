precision highp float;
precision highp sampler2D;

varying vec2 vUv;
uniform sampler2D u_velocity_texture;
uniform sampler2D u_input_texture;
uniform vec2 u_texel;
uniform float u_dt;
uniform float u_use_text;
uniform sampler2D u_text_texture;
uniform float u_dissipation;
uniform float u_dissipation_text;

vec4 bilerp (sampler2D sam, vec2 uv, vec2 tsize) {
	vec2 st = uv / tsize - 0.5;

	vec2 iuv = floor(st);
	vec2 fuv = fract(st);

	vec4 a = texture2D(sam, (iuv + vec2(0.5, 0.5)) * tsize);
	vec4 b = texture2D(sam, (iuv + vec2(1.5, 0.5)) * tsize);
	vec4 c = texture2D(sam, (iuv + vec2(0.5, 1.5)) * tsize);
	vec4 d = texture2D(sam, (iuv + vec2(1.5, 1.5)) * tsize);

	return mix(mix(a, b, fuv.x), mix(c, d, fuv.x), fuv.y);
}

void main () {
	vec2 coord = vUv - u_dt * bilerp(u_velocity_texture, vUv, u_texel).xy * u_texel;
	float text = texture2D(u_text_texture, vec2(vUv.x, 1. - vUv.y)).r;
	float dissipation = u_dissipation + text * u_dissipation_text * u_use_text;

	gl_FragColor = dissipation * bilerp(u_input_texture, coord, u_texel);
	gl_FragColor.a = 1.;
}